def vJavaLang = '1.8'
def javaProjects = [ project(':sagan-site'), project(':sagan-indexer'), project(':sagan-common') ]
def bootProjects = javaProjects - project(':sagan-common')
def gradleDir = "${rootProject.rootDir}/gradle"

wrapper.gradleVersion = '2.13'

buildscript {
    ext.springRepo = 'https://repo.spring.io/libs-release'

    repositories {
        maven { url springRepo }
    }

    dependencies {
		classpath 'io.spring.gradle:dependency-management-plugin:0.5.6.RELEASE'
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.3.3.RELEASE"
        classpath 'org.cloudfoundry:cf-gradle-plugin:1.1.3' // see deploy.gradle
    }
}

configure(allprojects) {
    apply plugin: 'eclipse'
}

configure(javaProjects) {
    apply plugin: 'java'

    targetCompatibility = vJavaLang
    sourceCompatibility = vJavaLang

    repositories {
        maven { url springRepo }
    }

    // Override Spring IO Platform versions
    ext['elasticsearch.version'] = '1.7.1' // see io.searchbox:jest

    dependencies {
        compile 'org.slf4j:slf4j-api'
        testCompile 'junit:junit'
        testCompile 'org.hamcrest:hamcrest-library'
        testCompile 'org.mockito:mockito-core'
        testCompile 'org.objenesis:objenesis'
    }

    configurations {
        // replaced with jcl-over-slf4j
        all*.exclude group: 'commons-logging', module: 'commons-logging'
        // replaced with log4j-over-slf4j
        all*.exclude group: 'log4j', module: 'log4j'
    }

    // Ensure that all Gradle-compiled classes are available to Eclipse's
    // classpath.
    eclipseClasspath.dependsOn testClasses

    // Skip generation and removal of .settings/org.eclipse.jdt.core.prefs files
    // during the normal `gradle eclipse` / `gradle cleanEclipse` lifecycle, as
    // these files have been checked in with formatting settings imported from
    // style/sagan-format.xml and style/sagan.importorder.
    // See https://www.gradle.org/docs/current/userguide/eclipse_plugin.html
    eclipseJdt.onlyIf { false }
    cleanEclipseJdt.onlyIf { false }
}

configure(bootProjects) {
	apply plugin: "io.spring.dependency-management"
    apply plugin: 'spring-boot'
    apply from: "$gradleDir/integTest.gradle"
    apply from: "$gradleDir/deploy.gradle"

    springBoot.backupSource = false

    dependencyManagement {
        imports {
            mavenBom 'io.spring.platform:platform-bom:2.0.3.RELEASE'
        }
    }
}

