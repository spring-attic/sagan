buildscript {

    ext.vHttpClient = '4.3'

    repositories {
        maven { url 'http://repo.spring.io/libs-milestone' }
    }

    dependencies {
        classpath "org.springframework.boot:spring-boot-gradle-plugin:1.0.0.RC2"
        classpath 'org.cloudfoundry:cf-gradle-plugin:1.0.2'
        classpath 'org.ajoberstar:gradle-git:0.6.3'
    }

}

apply plugin: 'eclipse'
apply plugin: 'idea'
idea.project {
    languageLevel = '1.7'
    ipr {
        withXml { provider ->
            def mapping = provider.node.component.find { it.@name == 'VcsDirectoryMappings' }.mapping
            mapping.@vcs = 'Git'
            mapping.@directory = '$PROJECT_DIR$'
        }
    }
}

subprojects {
    apply plugin: 'java'
    apply plugin: 'idea'
    apply plugin: 'eclipse'
    apply plugin: 'spring-boot'

    targetCompatibility = 1.7
    sourceCompatibility = 1.7

    repositories {
        maven { url 'http://repo.spring.io/libs-milestone' }
        mavenLocal()
    }

    dependencies {
        compile 'org.slf4j:slf4j-api'
        testCompile "junit:junit"
        testCompile 'org.hamcrest:hamcrest-library'
        testCompile 'org.mockito:mockito-core'
    }

}

configure([ project(':sagan-site'), project(':sagan-indexer') ]) {
    // Populate git commit and branch metadata at http://spring.io/info.
    // See:
    //   http://projects.spring.io/spring-boot/docs/spring-boot-actuator/docs/Features.html ("Info Endpoint")
    //   http://ajoberstar.org/gradle-git/docs/groovydoc/org/ajoberstar/gradle/git/tasks/GitBranchList.html
    //   https://github.com/ajoberstar/gradle-git
    task gitProperties(type: org.ajoberstar.gradle.git.tasks.GitBranchList) << {
        Properties p = new Properties()
        p['git.branch'] = workingBranch.name
        p['git.commit.id'] = workingBranch.commit.id
        p['git.commit.time'] = "N/A"
        p.store(new FileWriter(file("${sourceSets.main.output.resourcesDir}/git.properties")),
            "Written by the '${it.name}' Gradle build task")
    }
    processResources.finalizedBy gitProperties
}
