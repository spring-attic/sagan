apply plugin: 'spring-boot'
apply plugin: 'application'
apply from: '../gradle/integTest.gradle'
apply from: '../gradle/deploy.gradle'

mainClassName = 'sagan.app.site.ApplicationConfiguration'

// Avoid PermGen errors during JRuby/Asciidoctor guide processing on JDK 7 with `gradle :sagan-site:run`
// http://www.gradle.org/docs/1.7/release-notes#specify-default-jvm-arguments-for-the-application-plugin
applicationDefaultJvmArgs = [ "-XX:MaxPermSize=128M" ]

run {
    systemProperties System.properties
}

springBoot {
    backupSource = false
    mainClass = mainClassName
}

dependencies {
    compile project(':sagan-common')

    compile "org.springframework.boot:spring-boot-starter-web:${vSpringBoot}"
    compile "org.springframework.boot:spring-boot-starter-actuator:${vSpringBoot}"
    compile "org.springframework.boot:spring-boot-starter-security:${vSpringBoot}"
    compile "org.springframework.security:spring-security-acl:${vSpringSec}"

    // Thymeleaf is used as the template engine for serving HTML
    // NOTE: versions below are intended to be in sync with spring-boot-dependencies pom @ ${vSpringBoot}
    // e.g.: https://github.com/spring-projects/spring-boot/blob/v0.5.0.M6/spring-boot-dependencies/pom.xml
    compile 'org.thymeleaf:thymeleaf-spring3:2.1.2.RELEASE'
    compile 'org.thymeleaf:thymeleaf-spring4:2.1.2.RELEASE'
    compile 'org.thymeleaf:thymeleaf:2.1.2.RELEASE'
    compile 'nz.net.ultraq.thymeleaf:thymeleaf-layout-dialect:1.2'
    compile 'com.github.mxab.thymeleaf.extras:thymeleaf-extras-data-attribute:1.2'
    compile 'org.thymeleaf.extras:thymeleaf-extras-springsecurity3:2.1.1.RELEASE'

    // nekohtml is required for thymeleaf's LEGACYHTML5 mode
    runtime 'net.sourceforge.nekohtml:nekohtml:1.9.15'

    // for use in generating blog atom feeds
    compile 'net.java.dev.rome:rome:1.0.0'

    // for use by spring-social-github's OAuth2 support
    // note that 'runtime' scope would be enough, but 'compile'
    // helps override conflicting httpclient versions
    compile "org.apache.httpcomponents:httpclient:${vHttpClient}"
    compile "org.apache.httpcomponents:httpcore:${vHttpClient}"
    compile "org.apache.httpcomponents:httpcore-nio:${vHttpClient}"

    // for use in serving redirects; see urlrewrite.xml
    compile 'org.tuckey:urlrewritefilter:4.0.3'

    // for use in mocking http interactions
    testCompile "org.springframework:spring-test:${vSpringCore}"

    // pick up common test utility classes
    testCompile project(':sagan-common').sourceSets.testUtil.output
}

task copyGeneratedResources(type: Copy) {
    description = "Copies static html templates from :sagan-site in order to index them at runtime."

    from "../sagan-client/build"
    into sourceSets.main.output.resourcesDir
    include "**/*"
}